#pragma kernel FrustumCullMain
#define KERNEL_SIZE_X 64

struct InstancePara
{
	float4x4 model;
	float4 padding_for_other_para;
};
StructuredBuffer<InstancePara> _InstanceBuffer;
RWStructuredBuffer<uint> _ArgsBuffer;
RWStructuredBuffer<uint> _VisibilityBuffer;
uint _Count;
float3 _BoundsCenter;
float3 _BoundsExtent;
float4 _CullingPlanes[6];

inline bool PlaneTest(float4 planeWorld, float3 boundsCenterLocal, float3 boundsExtentLocal, float4x4 modelTranspose)
{
	float4 planeLocal = mul(modelTranspose, planeWorld);
	float3 normalAbs = abs(planeLocal.xyz);
	float radius = dot(normalAbs, boundsExtentLocal);
	float dist = dot(planeLocal.xyz, boundsCenterLocal) + planeLocal.w;
	return radius + dist > 0;
}

[numthreads(KERNEL_SIZE_X, 1, 1)]
void FrustumCullMain(uint3 id : SV_DispatchThreadID)
{
	if (id.x < _Count)
	{
		float4x4 mT = transpose(_InstanceBuffer[id.x].model);
		[unroll]
		for (int i = 0; i < 6; i++)
		{
			if (!PlaneTest(_CullingPlanes[i], _BoundsCenter, _BoundsExtent, mT))
			{
				return;
			}
		}
		int currentIndex = 0;
		InterlockedAdd(_ArgsBuffer[1], 1, currentIndex);
		_VisibilityBuffer[currentIndex] = id.x;
	}
}